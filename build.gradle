apply plugin: "groovy"
apply plugin: "java"
apply plugin: "application"

repositories {
    mavenCentral()
}

sourceCompatibility = '17'
targetCompatibility = '17'

dependencies {
    implementation 'org.apache.groovy:groovy:4.0.14'
    testImplementation 'io.cucumber:cucumber-java:7.13.0'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.13.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testImplementation 'org.junit.platform:junit-platform-suite-api:1.8.0'
    compileOnly 'org.projectlombok:lombok:1.18.28'
    annotationProcessor 'org.projectlombok:lombok:1.18.28'
    testCompileOnly 'org.projectlombok:lombok:1.18.28'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.28'
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

application {
    mainClassName = "com.gildedrose.TexttestFixture"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.register('cucumber') {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = [
                    '--plugin', 'pretty',
                    '--plugin', 'html:target/cucumber-report.html',
                    '--glue', 'com.gildedrose',
                    'src/test/resources']
        }
    }
}
